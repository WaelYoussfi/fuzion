# This file is part of the Fuzion language implementation.
#
# The Fuzion language implementation is free software: you can redistribute it
# and/or modify it under the terms of the GNU General Public License as published
# by the Free Software Foundation, version 3 of the License.
#
# The Fuzion language implementation is distributed in the hope that it will be
# useful, but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
# License for more details.
#
# You should have received a copy of the GNU General Public License along with The
# Fuzion language implementation.  If not, see <https://www.gnu.org/licenses/>.


# -----------------------------------------------------------------------
#
#  Tokiwa GmbH, Berlin
#
#  Source code of Fuzion standard library feature codepoint
#
#  Author: Fridtjof Siebert (siebert@tokiwa.eu)
#
# -----------------------------------------------------------------------

# codepoint represents a unicode code point
#
codepoint(val u32) : string
  pre
    debug: range.contains(val),
    debug: !utf16Surrogate.contains val
is

  # range of permitted value for a codepoint
  #
  range => 0.as_u32 .. 0x10ffff


  # range reserved for utf16 surrogate pairs
  #
  utf16Surrogate => 0xd800.as_u32 .. 0xdfff


  # range of values encoded in one byte
  #
  encodedInOneByte => 0.as_u32 .. 0x7f


  # range of values encoded in two bytes
  #
  encodedInTwoBytes => 0x80.as_u32 .. 0x7ff


  # range of values encoded in three bytes
  #
  encodedInThreeBytes => 0x800.as_u32 .. 0xffff


  # range of values encoded in four bytes
  #
  encodedInFourBytes => 0x10000.as_u32 .. 0x10ffff


  # the utf8 encoded bytes for the string representation
  # of this codepoint
  #
  redef asBytes streamable<i32> is
    v := val.as_i32
    if      encodedInOneByte   .contains val then [ val.as_i32 ]
    else if encodedInTwoBytes  .contains val then [ 0xc0 | (v >>  6) & 0x1f,
                                                    0x80 |  v        & 0x3f ]
    else if encodedInThreeBytes.contains val then [ 0xe0 | (v >> 12) & 0x1f,
                                                    0x80 | (v >>  6) & 0x3f,
                                                    0x80 |  v        & 0x3f ]
    else if encodedInFourBytes .contains val then [ 0xf0 | (v >> 18) & 0x07,
                                                    0x80 | (v >> 12) & 0x3f,
                                                    0x80 | (v >>  6) & 0x3f,
                                                    0x80 |  v        & 0x3f ]
    else
      fuzion.std.panic "failed to encode code point " + codepoint.this
